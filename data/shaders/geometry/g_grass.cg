#version 330 core

layout(points) in;
in float rotationY[];
in float scale[];

layout(triangle_strip, max_vertices = 10) out;
out vec2 textureCoords;
out float fogVisibility;
out vec4 shadowCoords;
out vec4 outColor;
out float alphaFactor;
out vec3 eyeDirection;
out vec3 lightDirection;

uniform mat4 P;
uniform mat4 V;
uniform float tile;

uniform float fogDensity;
uniform float fogGradient;
uniform vec2 size;

uniform vec3 lightPositionWorldspace;
uniform vec3 cameraPosition;

uniform vec4 clippingPlane;

uniform mat4 toShadowMapSpace;

uniform float timer;
uniform sampler2D textureWind;
uniform vec3 windModificators[1];

const float shadowDistance = 20.0;
const float transitionDistance = 40.0;
const float AFFECTED_BY_WIND_RADIUS = 20.0;
const float FAR_DISTANCE_CLIPPING = 40.0;
const float FOG_DENSITY_MULTIPLIER = 0.1;
const float PATCH_SIZE = 10.0f; //debería venir como un uniform


void createVertex(vec3 offset, vec2 coordText)
{
	vec4 actualOffset = vec4(offset, 0.0);
	vec4 worldPosition = (gl_in[0].gl_Position + actualOffset);
	shadowCoords = toShadowMapSpace * worldPosition;
	vec4 positionRelativeToCamera = V * worldPosition;
	gl_Position = P * positionRelativeToCamera;

	gl_ClipDistance[0] = dot(worldPosition, clippingPlane);

	//texture
	textureCoords = vec2(coordText.x, coordText.y) * vec2(tile, tile);

	//fog
	float distance = length(positionRelativeToCamera);
	fogVisibility = clamp(exp(-pow(distance * fogDensity, fogGradient)), 0.0, 1.0);

	//shadow
	distance = distance - (shadowDistance - transitionDistance);
	distance = distance / transitionDistance;
	shadowCoords.w = clamp(1.0 - distance, 0.0, 1.0);
	
	//camera data
	lightDirection = normalize(lightPositionWorldspace);
	eyeDirection = normalize(cameraPosition - worldPosition.xyz);

	EmitVertex();
}

vec3 calculateWindVector(float patchSize)
{
	vec2 coord = vec2(gl_in[0].gl_Position.x, gl_in[0].gl_Position.z) + vec2(patchSize * 0.5, patchSize * 0.5);
	coord = coord * 1.0 / patchSize;
	vec2 coord1 = coord * vec2(cos(timer + 4), sin(timer * 3 + 10));

	vec3 wind1 = texture2D(textureWind, coord1).bgr * 2.0 - 1.0;
	vec3 wind2 = texture2D(textureWind, coord1.yx).bgr * 2.0 - 1.0;
	vec3 wind = wind1 * 0.7 - wind2 * 0.25;
	wind.y = 0.8;

	wind = normalize(wind);
	return wind;
}

vec3 calculateModificatorsWindVector()
{
	bool modified = false;
	vec3 modification = vec3(0.0, 0.0, 0.0);

	for(int modificator = 0; modificator < 1; modificator++)
	{
		vec3 direction = gl_in[0].gl_Position.xyz -  windModificators[modificator];
		float distance = length(direction);
		if(distance < 0.3)
		{
			modification += (direction * 10.0 / distance);
			modified = true;
		}
	}
	if(modified)
	{
		modification = normalize(modification);
	}
	modification.y = max(0.0, modification.y);
	return modification;
}

vec3 toBezier(float delta, vec3 P0, vec3 P1, vec3 P2, vec3 P3)
{
    float t = delta;
    float t2 = t * t;
    float one_minus_t = 1.0 - t;
    float one_minus_t2 = one_minus_t * one_minus_t;
    return (P0 * one_minus_t2 * one_minus_t + P1 * 3.0 * t * one_minus_t2 + P2 * 3.0 * t2 * one_minus_t + P3 * t2 * t);
}

void main()
{
	vec4 positionRelativeToCamera = V * gl_in[0].gl_Position;
	float distance = length(positionRelativeToCamera);
	//clipping
	if(distance < 0.0 || distance > FAR_DISTANCE_CLIPPING)
	{
		return;
	}

	vec3 newWind = vec3(0.0, 1.0, 0.0);
	
	if(distance < AFFECTED_BY_WIND_RADIUS)
	{
		newWind = calculateWindVector(PATCH_SIZE);
		newWind += calculateModificatorsWindVector();
		newWind.x = 0.0f;
		newWind = normalize(newWind);
	}
	
	alphaFactor = clamp(exp(-pow(distance * FOG_DENSITY_MULTIPLIER, fogGradient)), 0.0, 1.0);

	vec3 dif = -newWind;
	dif.y = 0.0f;
	vec3 controlPoints[4];
	controlPoints[0] = vec3(0.0);
	controlPoints[1] = newWind + dif * 0.75;
	controlPoints[1].y = newWind.y / 2.0;
	controlPoints[2] = newWind + dif * 0.5;
	controlPoints[2].y = controlPoints[1].y + newWind.y / 4.0;
	controlPoints[3] = newWind;

	float x = size.x * 0.5 * scale[0];
	float h = size.y * 0.5 * scale[0];
	float sinAroundY = sin(rotationY[0]);
	float cosAroundY = cos(rotationY[0]);

	for(int c = 0; c <= 4; c++)
	{
		vec3 wind = toBezier(c/4.0, controlPoints[0], controlPoints[1], controlPoints[2], controlPoints[3]);
		vec3 vertex1 = vec3(-x, 0.0f, 0.0f) + wind * h;
		vec3 vertex2 = vec3(x, 0.0f, 0.0f) + wind * h;

		//rotate around Y
		vec3 vertex1Rotated = vec3(0.0f, vertex1.y, 0.0f);
		vec3 vertex2Rotated = vec3(0.0f, vertex2.y, 0.0f);

		vertex1Rotated.x = vertex1.x * cosAroundY + vertex1.z * sinAroundY;
		vertex1Rotated.z = vertex1.z * cosAroundY - vertex1.x * sinAroundY;

		vertex2Rotated.x = vertex2.x * cosAroundY + vertex2.z * sinAroundY;
		vertex2Rotated.z = vertex2.z * cosAroundY - vertex2.x * sinAroundY;
		
		createVertex(vertex1Rotated, vec2(0.0, vertex1Rotated.y / (newWind.y * h)));
		createVertex(vertex2Rotated, vec2(1.0, vertex2Rotated.y / (newWind.y *h)));
	}
	EndPrimitive();
}