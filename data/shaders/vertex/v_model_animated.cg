#version 330 core

in mat4 M;
in vec3 vertexModelspace;
in vec2 textureCoordsModelspace;
in vec3 normalModelspace;
in vec3 tangentModelspace;
in vec4 weights;
in ivec4 jointIndices;


out vec2 textureCoords;
out vec3 eyeDirection;
out vec3 lightDirection;
out vec3 positionWorldspace;
out float fogVisibility;
out float distanceToCamera;

out vec3 normal;
out vec4 shadowCoords;

uniform mat4 V;
uniform mat4 P;
uniform vec3 lightPositionWorldspace;
uniform vec3 cameraPosition;
uniform float fogDensity;
uniform float fogGradient;
uniform float tile;
uniform mat4 toShadowMapSpace;
uniform vec4 clippingPlane;
uniform mat4 jointTransforms[4];

const float shadowDistance = 10.0;
const float transitionDistance = 20.0;

void main()
{
	textureCoords = tile * textureCoordsModelspace;
	
	vec4 totalLocalPos = vec4(0.0);
	vec4 totalNormal = vec4(0.0);
	for(int i = 0; i < 4; ++i)
	{
		vec4 localPosition = jointTransforms[jointIndices[i]] * vec4(vertexModelspace, 1.0);
		totalLocalPos += localPosition * weights[i];
		
		vec4 worldNormal = jointTransforms[jointIndices[i]] * vec4(normalModelspace, 1.0);
		totalNormal += worldNormal * weights[i];
	}

	//totalLocalPos = vec4(vertexModelspace, 1.0);
	//totalNormal = vec4(normalModelspace, 1.0);

	positionWorldspace = vec3(M * totalLocalPos);
	shadowCoords = toShadowMapSpace * vec4(positionWorldspace, 1.0f);

	gl_ClipDistance[0] = dot(vec4(positionWorldspace, 1.0), clippingPlane);

	vec4 positionRelativeToCamera = V * vec4(positionWorldspace, 1.0f);	
	gl_Position =  P * positionRelativeToCamera;
	
	normal = normalize(vec3( M * totalNormal));
	
	lightDirection = normalize(lightPositionWorldspace - positionWorldspace);
	eyeDirection = normalize(cameraPosition - positionWorldspace);

	//fog
	distanceToCamera = length(positionRelativeToCamera);
	fogVisibility = clamp(exp(-pow(distanceToCamera * fogDensity, fogGradient)), 0.0, 1.0);

	//shadow
	float distance = distanceToCamera - (shadowDistance - transitionDistance);
	distance = distance / transitionDistance;
	shadowCoords.w = clamp(1.0 - distance, 0.0, 1.0);
}
