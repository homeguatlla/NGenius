#version 330 core

in vec3 vertexModelspace;
in vec2 textureCoordsModelspace;
in vec3 normalModelspace;
in vec3 tangentModelspace;
in mat4 M;

out vec2 textureCoords;
out vec3 eyeDirection;
out vec3 lightDirection;
out vec3 positionWorldspace;
out float fogVisibility;

out vec3 normal;

uniform mat4 V;
uniform mat4 P;
uniform vec3 lightPositionWorldspace;
uniform vec3 cameraPosition;
uniform float fogDensity;
uniform float fogGradient;
uniform float tile;

void main()
{
	textureCoords = tile * textureCoordsModelspace;
	
	positionWorldspace = vec3(M * vec4(vertexModelspace, 1.0));

	vec4 positionRelativeToCamera = V * vec4(positionWorldspace, 1.0f);	
	gl_Position =  P * positionRelativeToCamera;
	
	normal = normalize(vec3( M * vec4(normalModelspace, 0.0f) ));
	vec3 tangent = normalize(vec3( M * vec4(tangentModelspace, 0.0f) ));
	vec3 bitangent = normalize(cross(normal, tangent));

	mat3 toTangentSpace = mat3(
		tangent.x, bitangent.x, normal.x,
		tangent.y, bitangent.y, normal.y,
		tangent.z, bitangent.z, normal.z
	);

	lightDirection = toTangentSpace * normalize(lightPositionWorldspace - positionWorldspace);
	eyeDirection = toTangentSpace * normalize(cameraPosition - positionWorldspace);

	float distance = length(positionRelativeToCamera);
	fogVisibility = clamp(exp(-pow(distance * fogDensity, fogGradient)), 0.0, 1.0);
}
